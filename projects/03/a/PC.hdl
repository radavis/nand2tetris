// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Inc16(in = reg, out = rPlusOne);

    Mux16(
        a = reg,
        b = rPlusOne,
        sel = inc,
        out = muxA
    );

    Mux16(
        a = muxA,
        b = in,
        sel = load,
        out = muxB
    );

    Mux16(
        a = muxB,
        b = false,
        sel = reset,
        out = muxC
    );

    Register(
        in = muxC,
        load = true,
        out = reg,
        out = out
    );
}
