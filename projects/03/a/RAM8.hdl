// File name: projects/03/a/RAM8.hdl

/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    DMux8Way(
        in = load,
        sel = address,
        a = selR0,
        b = selR1,
        c = selR2,
        d = selR3,
        e = selR4,
        f = selR5,
        g = selR6,
        h = selR7
    );

    Register(in = in, load = selR0, out = r0);
    Register(in = in, load = selR1, out = r1);
    Register(in = in, load = selR2, out = r2);
    Register(in = in, load = selR3, out = r3);
    Register(in = in, load = selR4, out = r4);
    Register(in = in, load = selR5, out = r5);
    Register(in = in, load = selR6, out = r6);
    Register(in = in, load = selR7, out = r7);

    Mux8Way16(
        a = r0,
        b = r1,
        c = r2,
        d = r3,
        e = r4,
        f = r5,
        g = r6,
        h = r7,
        sel = address,
        out = out
    );
}
